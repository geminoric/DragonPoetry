class PortalBullet : ZilchComponent
{
    var BulletType : Real = 0; // 0 for Left Portal bullet, 1 for Right Portal bullet

    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, Events.CollisionStarted,   this.Collide);
    }
    
    function Init(bulletType : Real)
    {
        this.BulletType = bulletType;
        
        // Change sprite to match corresponding bullet
        if ( bulletType == 0 )
        {
            this.Owner.Sprite.Color = Real4(1.0, 1.0, 0.0, 1.0);
        }
        else
        {
            this.Owner.Sprite.Color = Real4(0.33, 0.23, 0.35, 1.0);
        }
        
    }
    
    function Collide(event : CollisionEvent)
    {
        // 0 = left-facing portal, 1 = down-facing, 2 = right-facing, 3 = top-facing
        if ( event.OtherObject.Sprite.SpriteSource == SpriteSource.Find("Normal Wall Left") )
        {
            this.CreatePortal(0, event.OtherObject);
        }
        else if ( event.OtherObject.Sprite.SpriteSource == SpriteSource.Find("Normal Wall Down") )
        {
            this.CreatePortal(1, event.OtherObject);
        }
        else if ( event.OtherObject.Sprite.SpriteSource == SpriteSource.Find("Normal Wall Right") )
        {
            this.CreatePortal(2, event.OtherObject);
        }
        else if ( event.OtherObject.Sprite.SpriteSource == SpriteSource.Find("Normal Wall Top") )
        {
            this.CreatePortal(3, event.OtherObject);
        }
        
        if ( event.OtherObject.Name != "DankBullet" && event.OtherObject.Name != "Player" )
        {
            this.Owner.Destroy();
        }
    }
    
    function CreatePortal(portalDirection : Real, collisionWall : Cog)
    {
        // Check if portal already exists on the wall
        var portalPos = Real3(collisionWall.Transform.Translation.X, collisionWall.Transform.Translation.Y, 2);
        if ( this.LevelSettings.LevelLoader.PortalLeft != null && this.LevelSettings.LevelLoader.PortalLeft.Transform.Translation == portalPos )
        {
            Console.WriteLine("Portal already exists on wall");
            return;
        }
        if ( this.LevelSettings.LevelLoader.PortalRight != null && this.LevelSettings.LevelLoader.PortalRight.Transform.Translation == portalPos )
        {
            Console.WriteLine("Portal already exists on wall");
            return;
        }
        
        // Make Portal
        var portal = this.Space.CreateAtPosition(Archetype.Find("Portal"), portalPos);
        portal.Portals.Init(this.BulletType, portalDirection);
        
        Console.WriteLine(this.LevelSettings.LevelLoader.PortalLeft);

        // Delete last portal and save new one
        if ( this.BulletType == 0 )
        {
            if ( this.LevelSettings.LevelLoader.PortalLeft != null )
            {
                this.LevelSettings.LevelLoader.PortalLeft.Destroy();
            }
            this.LevelSettings.LevelLoader.PortalLeft = portal;
            this.LevelSettings.LevelLoader.SetPos((portalPos.X + 0.5) as Integer, (portalPos.Y + 0.5) as Integer, 2, this.LevelSettings.LevelLoader.Extra);
        }
        else if ( this.BulletType == 1 )
        {
            if ( this.LevelSettings.LevelLoader.PortalRight != null )
            {
                this.LevelSettings.LevelLoader.PortalRight.Destroy();
            }
            this.LevelSettings.LevelLoader.PortalRight = portal;
            this.LevelSettings.LevelLoader.SetPos((portalPos.X + 0.5) as Integer, (portalPos.Y + 0.5) as Integer, 3, this.LevelSettings.LevelLoader.Extra);
        }
    }
    
    function OnLogicUpdate(event : UpdateEvent)
    {
    } 
}
