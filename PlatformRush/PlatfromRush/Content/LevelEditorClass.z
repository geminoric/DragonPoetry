class LevelEditorClass : ZilchComponent
{
    var LevelName : String = "testname";
    //Temporary name the level in the editor is saved as
    var EditorUN : String = "DONTUSETHISNAME452420789";
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        
        //Main buttons
        var but0 = this.Space.Create(Archetype.Find("Button"));
        but0.ButtonClass.Init(Real3(-19,8.3,0), Real3(1.5,1.5,1), this.New, "New", 30);
        var but1 = this.Space.Create(Archetype.Find("Button"));
        but1.ButtonClass.Init(Real3(-16.7,8.3,0), Real3(1.5,1.5,1), this.Save, "Save", 30);
        var but2 = this.Space.Create(Archetype.Find("Button"));
        but2.ButtonClass.Init(Real3(-14.4,8.3,0), Real3(1.5,1.5,1), this.Load, "Load", 30);
        var but3 = this.Space.Create(Archetype.Find("Button"));
        but3.ButtonClass.Init(Real3(-12.1,8.3,0), Real3(1.5,1.5,1), this.Test, "Test", 30);
        var but4 = this.Space.Create(Archetype.Find("Button"));
        but4.ButtonClass.Init(Real3(-9.8,8.3,0), Real3(1.5,1.5,1), this.Rename, "Rename", 25);
        var but5 = this.Space.Create(Archetype.Find("Button"));
        but5.ButtonClass.Init(Real3(3,8.3,0), Real3(1.5,1.5,1), this.Menu, "Menu", 30);
        var but6 = this.Space.Create(Archetype.Find("Button"));
        but6.ButtonClass.Init(Real3(0,8.3,0), Real3(1.5,1.5,1), this.Grid, "Grid", 30);
    }
    
    //Callback functions for buttons
    function Grid()
    {
        this.LevelSettings.EditorGridClass.GridSelect = !this.LevelSettings.EditorGridClass.GridSelect;
    }
    function New()
    {
        //Delete all the editor objects
        var objs = this.Space.FindAllObjectsByName("EditorObject");
        while(objs.IsNotEmpty)
        {
            objs.Current.Destroy();
            objs.MoveNext();
        }
        //Reset camera
        this.Space.FindObjectByName("Camera2").Transform.Translation = Real3(0, 500, 40);
    }
    function Save()
    {
        this.Space.FindObjectByName("saver").LevelSaveClass.SaveLevel();
    }
    function Menu()
    {
        this.Space.LoadLevel(Level.Find("MainMenu"));
    }
    function Load()
    {
        //Delete all the editor objects
        var objs = this.Space.FindAllObjectsByName("EditorObject");
        while(objs.IsNotEmpty)
        {
            objs.Current.Destroy();
            objs.MoveNext();
        }
        //Reset camera
        this.Space.FindObjectByName("Camera2").Transform.Translation = Real3(0, 500, 40);
        
        this.Space.FindObjectByName("saver").LevelSaveClass.LoadLevel(this.LevelName);
        this.Space.FindObjectByName("saver").LevelSaveClass.LoopLoaded(this.LoadIntoEditor);
    }
    function Test()
    {
        //Store current level name
        var tmpName = this.LevelName;
        //Swap level name for temporary file
        this.LevelName = this.EditorUN;
        //Save current map in temporary file
        this.Space.FindObjectByName("saver").LevelSaveClass.SaveLevel();
        //Switch name back
        this.LevelName = tmpName;
        //Load the level with the temporary name
        this.LevelSettings.LoadLevelClass.LoadLevel(this.EditorUN);
    }
    function Rename()
    {
        if(this.LevelSettings.RenameClass.Renaming)
        {
            this.LevelSettings.RenameClass.Renaming = false;
        }
        else
        {
            this.LevelSettings.RenameClass.CurName = this.LevelName;
            this.LevelSettings.RenameClass.Renaming = true;
        }
    }
    
    function LoadIntoEditor(obj : Cog)
    {
        //Player sprite
        if(obj.EditorObjectClass.TileID == 0)
        {
            this.Space.FindObjectByName("Player").Transform.Translation = obj.EditorObjectClass.Pos;
            obj.Destroy();
            return;
        }
        obj.Name = "EditorObject";
        obj.Sprite.SpriteSource = this.LevelSettings.EditorSprite.GetSprite(obj.EditorObjectClass.TileID);
    }
    
    
    function OnLogicUpdate(event : UpdateEvent)
    {
    }
}
